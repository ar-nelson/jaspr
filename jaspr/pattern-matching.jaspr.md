[â˜™ String Operations][prev] | [ğŸ— Table of Contents][toc] | [Signals and Error Handling â§][next]
:---|:---:|---:

# Pattern Matching

The `case`, `fn`, and `let` macros interpret certain forms as patterns, and match those patterns against values.

## Patterns

`null`, boolean, and number patterns match values equal to themselves, and do not bind any names.

String patterns that are valid names (not containing `.` or any reserved character, and not starting with `$`) match anything, and bind the matched value to the string. The string `â€œ_â€` (underscore) does not bind any variables, and can be used to ignore parts of a pattern.

Array patterns of length 2 starting with `â€œâ€` (quoted forms) match only values equal to the second element of the pattern, and do not bind any names.

Array patterns whose first element is not `â€œâ€` (the quote macro) and which do not contain the string `â€œ...â€` match arrays of the same length, after matching each element of the pattern to each element of the array.

Array patterns consisting of _n_ elements, then the string `â€œ...â€`, then a string that is a valid name (the _rest name_), match any array of length _n_ or greater and bind all except the first _n_ elements of the array to the rest name, after matching the first _n_ elements of the pattern to the first _n_ elements of the array.

> **â„¹ Tip:** Because unquoted strings are normalized (see [Unicode](syntax.jaspr.md#unicode)), the character `U+2026 HORIZONTAL ELLIPSIS (â€¦)` can be used interchangeably with the sequence `...`.

Object patterns match objects containing at least the same keys as the pattern, after matching the values of the pattern to the corresponding values of the object.

All other values are not legal patterns; using an illegal pattern in a pattern-matching macro will raise a `BadPattern` error at macro expansion time.

### Pattern Code Generation Functions

The pattern-matching macros use two internal, unexported functions to generate pattern-matching code: `makePatternTest` and `makePatternBindings`.

    makePatternTest:
    (fn- pat val
      (if (or (null? pat) (boolean? pat) (number? pat))
            `[p.is? ~val ~pat]
          (string? pat)
            true
          (object? pat)
            `[and (object? ~val)
                  (hasKeys? ~@(map quote (keys pat)) ~val)
                  ~@(map (\x makePatternTest (x pat) ([] (quote x) val))
                         (keys pat))]
          (and (= (len pat) 2) (emptyString? (0 pat)))
            `[= ~val ~pat]
          (and (>= (len pat) 2) (= â€œ...â€ (-2 pat)) (string? (-1 pat)))
            `[and (array? ~val)
                  (>= (len ~val) ~(sub (len pat) 2))
                  ~@(makeArray
                      (\x makePatternTest (x pat) ([] (quote x) val))
                      (sub (len pat) 2))]
          (none? (\ or (= â€œ...â€ _) (emptyString? _)) pat)
            `[and (array? ~val)
                  (= (len ~val) ~(len pat))
                  ~@(makeArray
                      (\x makePatternTest (x pat) ([] (quote x) val))
                      (len pat))]
          (raise {err: â€œBadPatternâ€, pattern: pat})))

    makePatternBindings:
    (fn- pat val
      (define {
        recur: (\->> (map (\x makePatternBindings (x pat) ([] (quote x) val)))
                     (apply merge))
      } (if (or (null? pat) (boolean? pat) (number? pat) (p.is? pat â€œ_â€))
              {}
            (string? pat)
              (if (or (emptyString? pat) (substring? â€œ.â€ pat))
                  (raise {err: â€œBadPatternâ€, pattern: pat})
                  ({} pat val))
            (object? pat)
              (recur (keys pat))
            (and (= (len pat) 2) (emptyString? (0 pat)))
              {}
            (and (>= (len pat) 2) (= â€œ...â€ (-2 pat)) (string? (-1 pat)))
              (->> (indexes pat)
                   (drop -2)
                   recur
                   (withKey (-1 pat) `[drop ~(sub (len pat) 2) ~val]))
            (recur (indexes pat)))))

## `case`

`(case x patâ‚€ exprâ‚€ patâ‚ exprâ‚ â€¦ patâ‚™ exprâ‚™)` matches each of the patterns `patâ‚€`â€¦`patâ‚™` to `x` until one of them matches, then evaluates and returns the corresponding `expr` in a scope where unquoted strings in the pattern are bound to the corresponding elements of `x`. Only one of the `expr`s is evaluated.

>     (case 1
>       0 â€œnoâ€
>       1 â€œyesâ€
>       1 â€œnoâ€) ;= â€œyesâ€

>     (case 42 x x) ;= 42

>     (case '[42] [x] x) ;= 42

>     (case â€œbarâ€
>       â€œfooâ€ â€œnoâ€
>       â€œbarâ€ â€œyesâ€
>       â€œbazâ€ â€œnoâ€) ;= â€œyesâ€

>     (case '[1 2 3]
>       []        â€œnoâ€
>       [1 2 3 4] â€œnoâ€
>       [1 x y]   `[~x ~y]) ;= [2, 3]

>     (case '[1 2 3]
>       [x â€¦ xs] `[~x ~xs]) ;= [1, [2, 3]]

>     (case []
>       [x â€¦ xs] â€œnoâ€
>       []       â€œyesâ€) ;= â€œyesâ€

>     (case {a: 1, b: 2}
>       {b c} â€œnoâ€
>       {a b} `[~a ~b]) ;= [1, 2]

>     (case {a: 1, b: 2}
>       {a}   â€œyesâ€
>       {a b} â€œnoâ€) ;= â€œyesâ€

>     (case {a: 1, b: 2}
>       {a: 2 b}    â€œnoâ€
>       {a: 1 b: 3} â€œnoâ€
>       {a: 1 b}    â€œyesâ€) ;= â€œyesâ€

`case` raises a `BadArgs` error at macro expansion time if it has an even number of arguments, a `BadPattern` error at macro expansion time if one of `patâ‚€`â€¦`patâ‚™` is not a legal pattern, or a `NoMatch` error at runtime if no pattern matches `x`.

---

    macro.case:
    (fn* exprs
      (define {
        valExpr: (hd exprs),
        useLet: (or (array? valExpr) (object? valExpr)),
        val: (if useLet (gensym!) valExpr),
        clauses: (->> (tl exprs)
                      (chunk 2)
                      (mapcat (fn- pair
                        (define {pat: (0 pair), expr: (1 pair)}
                          `[~(makePatternTest pat val)
                            (define ~(makePatternBindings pat val) ~expr)])))),
        ifExpr: `[if ~@clauses (raise {
                    err: â€œNoMatchâ€,
                    fn: ~(myName),
                    val: ~val
                  })]
      } (if useLet `[define ~({} val valExpr) ~ifExpr] ifExpr)))

## `fn`

`fn` defines a function using pattern matching. A `fn` form is made up of _clauses_ separated by the string `â€œ.â€`. Each clause is zero or more argument patterns, followed by a function body. When the function defined by `fn` is called, the array of arguments is matched against the argument patterns of each clause, in order, until one matches; if no clause matches, a `BadArgs` error is raised.

>     ((fn 42)) ;= 42

>     ((fn x (add 1 x)) 2) ;= 3

>     ((fn 1 'a
>        . 2 'b
>        . 3 'c) 2) ;= â€œbâ€

>     (define {
>       recursiveSum: (fn [] 0
>                       . [x â€¦ xs] (add x (recursiveSum xs)))
>     } (recursiveSum '[1 2 3 4])) ;= 10

---

    macro.fn:
    (fn* args
      `[fn* .args.
         (case .args.
           ~@(mapcat (fn- pat `[~(init pat) ~(last pat)])
                       (split â€œ.â€ args))
           args (raise { err: â€œBadArgsâ€, why: â€œno pattern match for argumentsâ€,
                         fn: (myName), args }))])

## `let`

`let` is the sequential, pattern-matching variant of `define`.

`(let patâ‚€ valâ‚€ patâ‚ valâ‚ â€¦ patâ‚™ valâ‚™ body)` evaluates `valâ‚€`â€¦`valâ‚™` in order, matching each pattern `pat` to the corresponding `val`, with the resulting bindings available when evaluating subsequent `val`s. It then returns the result of evaluating `body` with all bindings from all patterns in scope.

>     (let 42) ;= 42

>     (let x 91
>          x) ;= 91

>     (let [x y z] '[1 2 3]
>          y) ;= 2

>     (let x 1
>          y 2
>          {x y}) ;= {x: 1, y: 2}

>     (let [x â€¦ xs] '[1 2 3]
>          {x xs}) ;= {x: 1, xs: [2 3]}

Unlike `define`, `let` does not allow recursive definitions.

`let` raises a `BadArgs` error at macro expansion time if it has an even number of arguments, a `BadPattern` error at macro expansion time if one of `patâ‚€`â€¦`patâ‚™` is not a legal pattern, or a `NoMatch` error at runtime if any `val` does not match its corresponding pattern.

---

    macro.let:
    (fn body body
      . pat val â€¦ rest
        `[case ~val
           ~pat (let ~@rest)
           .value. (raise { err: â€œNoMatchâ€, fn: ~(myName),
                            pattern: ~(quote pat), value: .value. })])

## `awaitLet`

`awaitLet` is a combination of `let` and `await`.

`(awaitLet patâ‚€ valâ‚€ patâ‚ valâ‚ â€¦ patâ‚™ valâ‚™ body)` evaluates `valâ‚€`â€¦`valâ‚™` in order, matching each pattern `pat` to the corresponding `val`, with the resulting bindings available when evaluating subsequent `val`s.

>     (awaitLet 42) ;= 42

>     (awaitLet x 91
>               x) ;= 91

>     (awaitLet [x y z] '[1 2 3]
>               y) ;= 2

>     (awaitLet x 1
>               y 2
>               {x y}) ;= {x: 1, y: 2}

>     (awaitLet [x â€¦ xs] '[1 2 3]
>               {x xs}) ;= {x: 1, xs: [2 3]}

Each `val` is only evaluated after the previous `val` has resolved, as in `await`. Once all `val`s have resolved, `awaitLet` returns the result of evaluating `body` with all bindings from all patterns in scope.

>     (define {c: (chan!)}
>       (do (await (send! 10 c) (send! 20 c) (send! 30 c))
>           (awaitLet {value: x} (recv! c)
>                     {value: y} (recv! c)
>                     {value: z} (recv! c)
>                     ([] x y z)))) ;= [10, 20, 30]

`awaitLet` raises a `BadArgs` error at macro expansion time if it has an even number of arguments, a `BadPattern` error at macro expansion time if one of `patâ‚€`â€¦`patâ‚™` is not a legal pattern, or a `NoMatch` error at runtime if any `val` does not match its corresponding pattern.

---

    macro.awaitLet:
    (fn body body
      . pat val â€¦ rest
        `[define {.awaitLet.: ~val}
           (await .awaitLet.
                  (case .awaitLet.
                     ~pat (awaitLet ~@rest)
                     _ (raise { err: â€œNoMatchâ€, fn: ~(myName),
                                pattern: ~(quote pat), value: .awaitLet. })))])

## Exports

    $export: {case, fn, let, awaitLet, letAwait:awaitLet, ğŸ·:let}

[â˜™ String Operations][prev] | [ğŸ— Table of Contents][toc] | [Signals and Error Handling â§][next]
:---|:---:|---:

[toc]: jaspr.jaspr.md
[prev]: strings.jaspr.md
[next]: signals-errors.jaspr.md
